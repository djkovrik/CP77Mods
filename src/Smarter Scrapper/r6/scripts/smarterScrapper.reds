@if(ModuleExists("AtelierDelivery"))
import AtelierDelivery.OrderProcessingSystem

class SmarterScrapperClothesConfig {
  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53753")
  @runtimeProperty("ModSettings.category.order", "1")
  @runtimeProperty("ModSettings.displayName", "LocKey#1815")
  public let legendary: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53753")
  @runtimeProperty("ModSettings.category.order", "1")
  @runtimeProperty("ModSettings.displayName", "LocKey#1813")
  public let epic: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53753")
  @runtimeProperty("ModSettings.category.order", "1")
  @runtimeProperty("ModSettings.displayName", "LocKey#1816")
  public let rare: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53753")
  @runtimeProperty("ModSettings.category.order", "1")
  @runtimeProperty("ModSettings.displayName", "LocKey#1817")
  public let uncommon: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53753")
  @runtimeProperty("ModSettings.category.order", "1")
  @runtimeProperty("ModSettings.displayName", "LocKey#1814")
  public let common: Bool = true;
}

class SmarterScrapperWeaponsConfig {
  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53751")
  @runtimeProperty("ModSettings.category.order", "2")
  @runtimeProperty("ModSettings.displayName", "LocKey#1815")
  public let legendary: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53751")
  @runtimeProperty("ModSettings.category.order", "2")
  @runtimeProperty("ModSettings.displayName", "LocKey#1813")
  public let epic: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53751")
  @runtimeProperty("ModSettings.category.order", "2")
  @runtimeProperty("ModSettings.displayName", "LocKey#1816")
  public let rare: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53751")
  @runtimeProperty("ModSettings.category.order", "2")
  @runtimeProperty("ModSettings.displayName", "LocKey#1817")
  public let uncommon: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53751")
  @runtimeProperty("ModSettings.category.order", "2")
  @runtimeProperty("ModSettings.displayName", "LocKey#1814")
  public let common: Bool = true;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#53751")
  @runtimeProperty("ModSettings.displayName", "LocKey#778")
  public let knife: Bool = false;
}

class SmarterScrapperModsConfig {
  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#49863")
  @runtimeProperty("ModSettings.category.order", "3")
  @runtimeProperty("ModSettings.displayName", "LocKey#1815")
  public let legendary: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#49863")
  @runtimeProperty("ModSettings.category.order", "3")
  @runtimeProperty("ModSettings.displayName", "LocKey#1813")
  public let epic: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#49863")
  @runtimeProperty("ModSettings.category.order", "3")
  @runtimeProperty("ModSettings.displayName", "LocKey#1816")
  public let rare: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#49863")
  @runtimeProperty("ModSettings.category.order", "3")
  @runtimeProperty("ModSettings.displayName", "LocKey#1817")
  public let uncommon: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#49863")
  @runtimeProperty("ModSettings.category.order", "3")
  @runtimeProperty("ModSettings.displayName", "LocKey#1814")
  public let common: Bool = false;
}

class SmarterScrapperProgramsConfig {
  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#54090")
  @runtimeProperty("ModSettings.category.order", "4")
  @runtimeProperty("ModSettings.displayName", "LocKey#1815")
  public let legendary: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#54090")
  @runtimeProperty("ModSettings.category.order", "4")
  @runtimeProperty("ModSettings.displayName", "LocKey#1813")
  public let epic: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#54090")
  @runtimeProperty("ModSettings.category.order", "4")
  @runtimeProperty("ModSettings.displayName", "LocKey#1816")
  public let rare: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#54090")
  @runtimeProperty("ModSettings.category.order", "4")
  @runtimeProperty("ModSettings.displayName", "LocKey#1817")
  public let uncommon: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#54090")
  @runtimeProperty("ModSettings.category.order", "4")
  @runtimeProperty("ModSettings.displayName", "LocKey#1814")
  public let common: Bool = true;
}

class SmarterScrapperJunkConfig {
  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#46566")
  @runtimeProperty("ModSettings.category.order", "5")
  @runtimeProperty("ModSettings.displayName", "LocKey#245")
  public let enabled: Bool = true;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "LocKey#46566")
  @runtimeProperty("ModSettings.category.order", "5")
  @runtimeProperty("ModSettings.displayName", "LocKey#731")
  @runtimeProperty("ModSettings.step", "1")
  @runtimeProperty("ModSettings.min", "1")
  @runtimeProperty("ModSettings.max", "600")
  public let maxPrice: Int32 = 50;
}

class SmarterScrapperEdiblesConfig {
  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "Gameplay-Items-Item Type-Con_Edible")
  @runtimeProperty("ModSettings.category.order", "6")
  @runtimeProperty("ModSettings.displayName", "LocKey#1815")
  public let legendary: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "Gameplay-Items-Item Type-Con_Edible")
  @runtimeProperty("ModSettings.category.order", "6")
  @runtimeProperty("ModSettings.displayName", "LocKey#1813")
  public let epic: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "Gameplay-Items-Item Type-Con_Edible")
  @runtimeProperty("ModSettings.category.order", "6")
  @runtimeProperty("ModSettings.displayName", "LocKey#1816")
  public let rare: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "Gameplay-Items-Item Type-Con_Edible")
  @runtimeProperty("ModSettings.category.order", "6")
  @runtimeProperty("ModSettings.displayName", "LocKey#1817")
  public let uncommon: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "Gameplay-Items-Item Type-Con_Edible")
  @runtimeProperty("ModSettings.category.order", "6")
  @runtimeProperty("ModSettings.displayName", "LocKey#1814")
  public let common: Bool = false;

  @runtimeProperty("ModSettings.mod", "Scrapper")
  @runtimeProperty("ModSettings.category", "Gameplay-Items-Item Type-Con_Edible")
  @runtimeProperty("ModSettings.category.order", "6")
  @runtimeProperty("ModSettings.displayName", "LocKey#4933")
  @runtimeProperty("ModSettings.step", "1")
  @runtimeProperty("ModSettings.min", "1")
  @runtimeProperty("ModSettings.max", "20")
  public let craftComponents: Int32 = 1;
}

@addMethod(PlayerPuppet)
public func HasExcludedTagsSS(data: ref<gameItemData>) -> Bool {
  let tags: array<CName> = [ 
    n"Quest",
    n"PermanentFood", 
    n"PermanentStaminaFood", 
    n"PermanentMemoryFood", 
    n"PermanentHealthFood", 
    n"ChipwareExpansion.Chip"
  ];

  return data.HasAnyOfTags(tags);
}

@addMethod(PlayerPuppet)
public func IsExclusionSS(id: TweakDBID) -> Bool {
  return 
    // Stadium Love
    Equals(id, t"Items.Preset_MQ008_Nova") ||
    // Sasquatch Hammer
    Equals(id, t"Items.Preset_Hammer_Sasquatch") ||
    // Prologue
    Equals(id, t"Items.Preset_Nova_Q000_Nomad") ||
    // Egg
    Equals(id, t"Items.q005_iguana_egg") ||
    // Superfood
    Equals(id, t"Items.PermanentHealthFood") ||
    Equals(id, t"Items.PermanentStaminaRegenFood") ||
    Equals(id, t"Items.PermanentMemoryRegenFood") ||
    // Cat food
    Equals(id, t"Items.LowQualityFood11") ||
  false;
}

@addMethod(PlayerPuppet)
public func HasExcludedQuestActive() -> Bool {
  let journalManager: wref<JournalManager> = GameInstance.GetJournalManager(this.GetGame());
  let trackedObjective: wref<JournalQuestObjective> = journalManager.GetTrackedEntry() as JournalQuestObjective;
  return 
    // Starting tutorial
    Equals(trackedObjective.GetId(), "01a_pick_weapon") ||
    Equals(trackedObjective.GetId(), "01c_pick_up_reanimator") ||
    Equals(trackedObjective.GetId(), "03_pick_up_katana") ||
    Equals(trackedObjective.GetId(), "prepare_before_leave") ||
    // PL
    Equals(trackedObjective.GetId(), "00_get_your_gear") ||
    Equals(trackedObjective.GetId(), "01_talk_to_songbird") ||
    Equals(trackedObjective.GetId(), "01b_follow_songbird") ||
    Equals(trackedObjective.GetId(), "02_escape_vip_area") ||
    Equals(trackedObjective.GetId(), "03_escape_vip_area") ||
  false;
}

@addMethod(PlayerPuppet)
private func IsClothesSS(type: gamedataItemType) -> Bool {
  return
    Equals(type, gamedataItemType.Clo_Face) ||
    Equals(type, gamedataItemType.Clo_Feet) ||
    Equals(type, gamedataItemType.Clo_Head) ||
    Equals(type, gamedataItemType.Clo_InnerChest) ||
    Equals(type, gamedataItemType.Clo_Legs) ||
    Equals(type, gamedataItemType.Clo_OuterChest) ||
  false;
}

@addMethod(PlayerPuppet)
private func IsWeaponSS(type: gamedataItemType) -> Bool {
  return
    Equals(type, gamedataItemType.Wea_AssaultRifle) ||
    Equals(type, gamedataItemType.Wea_Hammer) ||
    Equals(type, gamedataItemType.Wea_Handgun) ||
    Equals(type, gamedataItemType.Wea_HeavyMachineGun) ||
    Equals(type, gamedataItemType.Wea_Katana) ||
    Equals(type, gamedataItemType.Wea_LightMachineGun) ||
    Equals(type, gamedataItemType.Wea_LongBlade) ||
    Equals(type, gamedataItemType.Wea_OneHandedClub) ||
    Equals(type, gamedataItemType.Wea_PrecisionRifle) ||
    Equals(type, gamedataItemType.Wea_Revolver) ||
    Equals(type, gamedataItemType.Wea_Rifle) ||
    Equals(type, gamedataItemType.Wea_ShortBlade) ||
    Equals(type, gamedataItemType.Wea_Shotgun) ||
    Equals(type, gamedataItemType.Wea_ShotgunDual) ||
    Equals(type, gamedataItemType.Wea_SniperRifle) ||
    Equals(type, gamedataItemType.Wea_SubmachineGun) ||
    Equals(type, gamedataItemType.Wea_TwoHandedClub) ||
    Equals(type, gamedataItemType.Wea_Axe) ||
    Equals(type, gamedataItemType.Wea_Chainsword) ||
    Equals(type, gamedataItemType.Wea_Machete) ||
  false;
}

@addMethod(PlayerPuppet)
private func IsModSS(type: gamedataItemType) -> Bool {
  return
    Equals(type, gamedataItemType.Prt_AR_SMG_LMGMod) ||
    Equals(type, gamedataItemType.Prt_BladeMod) ||
    Equals(type, gamedataItemType.Prt_BluntMod) ||
    Equals(type, gamedataItemType.Prt_BootsFabricEnhancer) ||
    Equals(type, gamedataItemType.Prt_Capacitor) ||
    Equals(type, gamedataItemType.Prt_FabricEnhancer) ||
    Equals(type, gamedataItemType.Prt_FaceFabricEnhancer) ||
    Equals(type, gamedataItemType.Prt_Fragment) ||
    Equals(type, gamedataItemType.Prt_HandgunMod) ||
    Equals(type, gamedataItemType.Prt_HandgunMuzzle) ||
    Equals(type, gamedataItemType.Prt_HeadFabricEnhancer) ||
    Equals(type, gamedataItemType.Prt_LongScope) ||
    Equals(type, gamedataItemType.Prt_Magazine) ||
    Equals(type, gamedataItemType.Prt_MeleeMod) ||
    Equals(type, gamedataItemType.Prt_Mod) ||
    Equals(type, gamedataItemType.Prt_Muzzle) ||
    Equals(type, gamedataItemType.Prt_OuterTorsoFabricEnhancer) ||
    Equals(type, gamedataItemType.Prt_PantsFabricEnhancer) ||
    Equals(type, gamedataItemType.Prt_PowerMod) ||
    Equals(type, gamedataItemType.Prt_PowerSniperScope) ||
    Equals(type, gamedataItemType.Prt_Precision_Sniper_RifleMod) ||
    Equals(type, gamedataItemType.Prt_RangedMod) ||
    Equals(type, gamedataItemType.Prt_Receiver) ||
    Equals(type, gamedataItemType.Prt_RifleMuzzle) ||
    Equals(type, gamedataItemType.Prt_Scope) ||
    Equals(type, gamedataItemType.Prt_ScopeRail) ||
    Equals(type, gamedataItemType.Prt_ShortScope) ||
    Equals(type, gamedataItemType.Prt_ShotgunMod) ||
    Equals(type, gamedataItemType.Prt_SmartMod) ||
    Equals(type, gamedataItemType.Prt_Stock) ||
    Equals(type, gamedataItemType.Prt_TargetingSystem) ||
    Equals(type, gamedataItemType.Prt_TechMod) ||
    Equals(type, gamedataItemType.Prt_TechSniperScope) ||
    Equals(type, gamedataItemType.Prt_ThrowableMod) ||
    Equals(type, gamedataItemType.Prt_TorsoFabricEnhancer) ||

  false;
}

@addMethod(PlayerPuppet)
private func IsQuickHackSS(type: gamedataItemType) -> Bool {
  return Equals(type, gamedataItemType.Prt_Program);
}

@addMethod(PlayerPuppet)
private func IsEdibleSS(type: gamedataItemType) -> Bool {
  return
    Equals(type, gamedataItemType.Con_Edible ) ||
  false;
}

@addField(PlayerPuppet) public let scrapperClothes: ref<SmarterScrapperClothesConfig>;
@addField(PlayerPuppet) public let scrapperWeapons: ref<SmarterScrapperWeaponsConfig>;
@addField(PlayerPuppet) public let scrapperMods: ref<SmarterScrapperModsConfig>;
@addField(PlayerPuppet) public let scrapperPrograms: ref<SmarterScrapperProgramsConfig>;
@addField(PlayerPuppet) public let scrapperJunk: ref<SmarterScrapperJunkConfig>;
@addField(PlayerPuppet) public let scrapperEdibles: ref<SmarterScrapperEdiblesConfig>;

@wrapMethod(PlayerPuppet)
protected cb func OnGameAttached() -> Bool {
  wrappedMethod();

  this.scrapperClothes = new SmarterScrapperClothesConfig();
  this.scrapperWeapons = new SmarterScrapperWeaponsConfig();
  this.scrapperMods = new SmarterScrapperModsConfig();
  this.scrapperPrograms = new SmarterScrapperProgramsConfig();
  this.scrapperJunk = new SmarterScrapperJunkConfig();
  this.scrapperEdibles = new SmarterScrapperEdiblesConfig();
}

@wrapMethod(PlayerPuppet)
protected cb func OnDetach() -> Bool {
  wrappedMethod();

  this.scrapperClothes = null;
  this.scrapperWeapons = null;
  this.scrapperMods = null;
  this.scrapperPrograms = null;
  this.scrapperJunk = null;
  this.scrapperEdibles = null;
}

@addMethod(PlayerPuppet)
private func ShouldBeScrappedSS(data: ref<gameItemData>, quality: gamedataQuality) -> Bool {
  let type: gamedataItemType = data.GetItemType();

  if this.ReceivedFromDropPoint(data) {
    return false;
  };

  if this.IsClothesSS(type) {
    switch quality {
      case gamedataQuality.LegendaryPlusPlus:
      case gamedataQuality.LegendaryPlus:
      case gamedataQuality.Legendary: 
        return this.scrapperClothes.legendary;
      case gamedataQuality.EpicPlus:
      case gamedataQuality.Epic: 
        return this.scrapperClothes.epic;
      case gamedataQuality.RarePlus:
      case gamedataQuality.Rare: 
        return this.scrapperClothes.rare;
      case gamedataQuality.UncommonPlus:
      case gamedataQuality.Uncommon: 
        return this.scrapperClothes.uncommon;
      case gamedataQuality.CommonPlus:
      case gamedataQuality.Common: 
        return this.scrapperClothes.common;
    };
  };

  if this.IsWeaponSS(type) {
    switch quality {
      case gamedataQuality.LegendaryPlusPlus:
      case gamedataQuality.LegendaryPlus:
      case gamedataQuality.Legendary:
        return this.scrapperWeapons.legendary;
      case gamedataQuality.EpicPlus:
      case gamedataQuality.Epic:
        return this.scrapperWeapons.epic;
      case gamedataQuality.RarePlus:
      case gamedataQuality.Rare:
        return this.scrapperWeapons.rare;
      case gamedataQuality.UncommonPlus:
      case gamedataQuality.Uncommon: 
        return this.scrapperWeapons.uncommon;
      case gamedataQuality.CommonPlus:
      case gamedataQuality.Common:
        return this.scrapperWeapons.common;
    };
  };

  if Equals(type, gamedataItemType.Wea_Knife) && this.scrapperWeapons.knife {
    switch quality {
      case gamedataQuality.Legendary: return this.scrapperWeapons.legendary;
      case gamedataQuality.Epic: return this.scrapperWeapons.epic;
      case gamedataQuality.Rare: return this.scrapperWeapons.rare;
      case gamedataQuality.Uncommon: return this.scrapperWeapons.uncommon;
      case gamedataQuality.Common: return this.scrapperWeapons.common;
    };
  };
  
  if this.IsModSS(type) {
    switch quality {
      case gamedataQuality.Rare: return this.scrapperMods.rare;
      case gamedataQuality.Uncommon: return this.scrapperMods.uncommon;
      case gamedataQuality.Common: return this.scrapperMods.common;
    };
  };

  if this.IsQuickHackSS(type) {
    switch quality {
      case gamedataQuality.Legendary: return this.scrapperPrograms.legendary;
      case gamedataQuality.Epic: return this.scrapperPrograms.epic;
      case gamedataQuality.Rare: return this.scrapperPrograms.rare;
      case gamedataQuality.Uncommon: return this.scrapperPrograms.uncommon;
      case gamedataQuality.Common: return this.scrapperPrograms.common;
    };
  }

  return false;
}

@addMethod(PlayerPuppet)
private func ShouldBeScrappedJunkSS(data: ref<gameItemData>) -> Bool {
  if this.ReceivedFromDropPoint(data) {
    return false;
  };

  let type: gamedataItemType = data.GetItemType();
  let price: Int32;
  if Equals(type, gamedataItemType.Gen_Junk) {
    price = RPGManager.CalculateSellPrice(this.GetGame(), this, data.GetID());
    return this.scrapperJunk.enabled && price < this.scrapperJunk.maxPrice ;
  };

  return false;
}

@addMethod(PlayerPuppet)
private func ShouldBeScrappedEdibleSS(data: ref<gameItemData>, quality: gamedataQuality) -> Bool {
  if this.ReceivedFromDropPoint(data) {
    return false;
  };

  let type: gamedataItemType = data.GetItemType();

  if !this.IsEdibleSS(type) {
    return false;
  }

  switch quality {
    case gamedataQuality.LegendaryPlusPlus:
    case gamedataQuality.LegendaryPlus:
    case gamedataQuality.Legendary:
      return this.scrapperEdibles.legendary;
    case gamedataQuality.EpicPlus:
    case gamedataQuality.Epic:
      return this.scrapperEdibles.epic;
    case gamedataQuality.RarePlus:
    case gamedataQuality.Rare:
      return this.scrapperEdibles.rare;
    case gamedataQuality.UncommonPlus:
    case gamedataQuality.Uncommon: 
      return this.scrapperEdibles.uncommon;
    case gamedataQuality.CommonPlus:
    case gamedataQuality.Common:
      return this.scrapperEdibles.common;
  };

  return false;
}

// Keep last bought item id
@addField(PlayerPuppet)
public let boughtItem: ItemID;

// Save last bought item id
@wrapMethod(Vendor)
private final func PerformItemTransfer(buyer: wref<GameObject>, seller: wref<GameObject>, const itemTransaction: script_ref<SItemTransaction>) -> Bool {
  let player: ref<PlayerPuppet> = buyer as PlayerPuppet;
  if IsDefined(player) {
    player.boughtItem = Deref(itemTransaction).itemStack.itemID;
  };
  return wrappedMethod(buyer, seller, itemTransaction);
}

@addMethod(PlayerPuppet)
public func HasWeaponInInventorySS() -> Bool {
  let itemList: array<wref<gameItemData>>;
  let item: wref<gameItemData>;
  GameInstance.GetTransactionSystem(this.GetGame()).GetItemList(this, itemList);
  let i: Int32 = 0;
  let counter: Int32 = 0;
  while i < ArraySize(itemList) {
    item = itemList[i];
    if this.IsWeaponSS(item.GetItemType()) && NotEquals(item.GetName(), n"w_handgun_constitutional_unity") {
      counter += 1;
    };
    i += 1;
  };
  return counter > 1;
}

// Runs when item added to inventory
@wrapMethod(PlayerPuppet)
protected cb func OnItemAddedToInventory(evt: ref<ItemAddedEvent>) -> Bool {
  wrappedMethod(evt);

  let ts: ref<TransactionSystem>;
  let gameItemData: wref<gameItemData> = evt.itemData;
  let tdbid: TweakDBID = ItemID.GetTDBID(gameItemData.GetID());
  let quality: gamedataQuality = RPGManager.GetItemDataQuality(gameItemData);

  if this.HasExcludedTagsSS(gameItemData) || this.IsExclusionSS(tdbid) || this.HasExcludedQuestActive() {
    return true;
  };

  let gear: Bool = this.HasWeaponInInventorySS() 
    && this.ShouldBeScrappedSS(gameItemData, quality) 
    && !RPGManager.IsItemIconic(gameItemData) 
    && NotEquals(this.boughtItem, gameItemData.GetID()) 
    && !RPGManager.IsItemCrafted(gameItemData);

  let junk: Bool = this.ShouldBeScrappedJunkSS(gameItemData);

  let edible: Bool = this.ShouldBeScrappedEdibleSS(gameItemData, quality) 
    && NotEquals(this.boughtItem, gameItemData.GetID());

  if gear || junk {
    ItemActionsHelper.DisassembleItem(this, evt.itemID, GameInstance.GetTransactionSystem(this.GetGame()).GetItemQuantity(this, evt.itemID));
  };

  if edible {
    ts = GameInstance.GetTransactionSystem(this.GetGame());
    ts.RemoveItem(this, gameItemData.GetID(), gameItemData.GetQuantity());
    ts.GiveItemByTDBID(this, t"Items.CommonMaterial1", this.scrapperEdibles.craftComponents);
  };
}

public class RefreshScapperConfigsEvent extends Event {}

@addMethod(PlayerPuppet)
protected cb func OnRefreshScapperConfigsEvent(evt: ref<RefreshScapperConfigsEvent>) -> Bool {
  this.scrapperClothes = new SmarterScrapperClothesConfig();
  this.scrapperWeapons = new SmarterScrapperWeaponsConfig();
  this.scrapperMods = new SmarterScrapperModsConfig();
  this.scrapperPrograms = new SmarterScrapperProgramsConfig();
  this.scrapperJunk = new SmarterScrapperJunkConfig();
  this.scrapperEdibles = new SmarterScrapperEdiblesConfig();
}

@wrapMethod(PauseMenuBackgroundGameController)
protected cb func OnUninitialize() -> Bool {
  this.GetPlayerControlledObject().QueueEvent(new RefreshScapperConfigsEvent());
  wrappedMethod();
}

@if(!ModuleExists("VendorPreview.Config"))
@addMethod(PlayerPuppet)
private func ReceivedFromDropPoint(data: ref<gameItemData>) -> Bool {
  return false;
}

@if(ModuleExists("VendorPreview.Config"))
@addMethod(PlayerPuppet)
private func ReceivedFromDropPoint(data: ref<gameItemData>) -> Bool {
  return OrderProcessingSystem.Get(this.GetGame()).IsItemPurchased(data.GetID());
}
